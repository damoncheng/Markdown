#Hypertext Transfer Protocol (HTTP/1.1): Message Syntax and Routing

## Uniform Resource Identifiers

	Uniform Resource Identifiers (URIs) [RFC3986] are used throughout
	HTTP as the means for identifying resources (Section 2 of [RFC7231]).
	URI references are used to target requests, indicate redirects, and
	define relationships.

[参考文献](https://tools.ietf.org/html/rfc7230#section-2.7)
	
	 URI-reference = <URI-reference, see [RFC3986], Section 4.1>
	 
	 absolute-URI  = <absolute-URI, see [RFC3986], Section 4.3>
	 relative-part = <relative-part, see [RFC3986], Section 4.2>
	 scheme        = <scheme, see [RFC3986], Section 3.1>
	 authority     = <authority, see [RFC3986], Section 3.2>
	 uri-host      = <host, see [RFC3986], Section 3.2.2>
	 port          = <port, see [RFC3986], Section 3.2.3>
	 path-abempty  = <path-abempty, see [RFC3986], Section 3.3>
	 segment       = <segment, see [RFC3986], Section 3.3>
	 query         = <query, see [RFC3986], Section 3.4>
	 fragment      = <fragment, see [RFC3986], Section 3.5>
	
	 absolute-path = 1*( "/" segment )
	 partial-URI   = relative-part [ "?" query ]
	 
### URI-reference

 URI-reference is used to denote the most common usage of a resource identifier.
 
 	URI-reference = URI / relative-ref

A URI-reference is either a URI or a relative reference.  If the
   URI-reference's prefix does not match the syntax of a scheme followed
   by its colon separator, then the URI-reference is a relative
   reference.
   
A URI-reference is typically parsed first into the five URI
   components, in order to determine what components are present and
   whether the reference is relative.  Then, each component is parsed
   for its subparts and their validation.  The ABNF of URI-reference,
   along with the "first-match-wins" disambiguation rule, is sufficient
   to define a validating parser for the generic syntax.  Readers
   familiar with regular expressions should see Appendix B for an
   example of a non-validating URI-reference parser that will take any
   given string and extract the URI components.
   
### relative-part

 A relative reference takes advantage of the hierarchical syntax
   (Section 1.2.3) to express a URI reference relative to the name space
   of another hierarchical URI.
	   
	  relative-ref  = relative-part [ "?" query ] [ "#" fragment ]
	
	  relative-part = "//" authority path-abempty
	                / path-absolute
	                / path-noscheme
	                / path-empty


The URI referred to by a relative reference, also known as the target
   URI, is obtained by applying the reference resolution algorithm of
   Section 5.
   
A relative reference that **begins with two slash characters is termed
a network-path reference**; such references are rarely used.  A
relative reference that **begins with a single slash character is
termed an absolute-path reference**.  A relative reference that **does
not begin with a slash character is termed a relative-path reference**.

A path segment that contains a colon character (e.g., "this:that")
cannot be used as the first segment of a relative-path reference, as
it would be mistaken for a scheme name.  Such a segment must be
preceded by a dot-segment (e.g., "./this:that") to make a relative-
path reference.

###  Absolute URI

Some protocol elements allow only the absolute form of a URI without
a fragment identifier.  For example, defining a base URI for later
use by relative references calls for an absolute-URI syntax rule that
does not allow a fragment.
   
   	absolute-URI  = scheme ":" hier-part [ "?" query ]
   	
URI scheme specifications must define their own syntax so that all
strings matching their scheme-specific syntax will also match the
<absolute-URI> grammar.  Scheme specifications will not define
fragment identifier syntax or usage, regardless of its applicability
to resources identifiable via that scheme, as fragment identification
is orthogonal to scheme definition.  However, scheme specifications
are encouraged to include a wide range of examples, including
examples that show use of the scheme's URIs with fragment identifiers
when such usage is appropriate.

### Scheme

Scheme names consist of a sequence of characters beginning with a
   letter and followed by any combination of letters, digits, plus
   ("+"), period ("."), or hyphen ("-").  Although schemes are case-
   insensitive, the canonical form is lowercase and documents that
   specify schemes must do so with lowercase letters.  An implementation
   should accept uppercase letters as equivalent to lowercase in scheme
   names (e.g., allow "HTTP" as well as "http") for the sake of
   robustness but should only produce lowercase scheme names for
   consistency.
   
    scheme      = ALPHA *( ALPHA / DIGIT / "+" / "-" / "." )
    
### Authority

Many URI schemes include a hierarchical element for a naming
authority so that governance of the name space defined by the
remainder of the URI is delegated to that authority (which may, in
turn, delegate it further).  The generic syntax provides a common
means for distinguishing an authority based on a registered name or
server address, along with optional port and user information.

The authority component is preceded by a double slash ("//") and is
terminated by the next slash ("/"), question mark ("?"), or number
sign ("#") character, or by the end of the URI.
   
   	authority   = [ userinfo "@" ] host [ ":" port ]

If a URI contains an authority component, then the path component
must either be empty or begin with a slash ("/") character.  Non-
validating parsers (those that merely separate a URI reference into
its major components) will often ignore the subcomponent structure of
authority, treating it as an opaque string from the double-slash to
the first terminating delimiter, until such time as the URI is
dereferenced.

### host

	host        = IP-literal / IPv4address / reg-name

### port

	port        = *DIGIT
	
### path

The path component contains data, usually organized in hierarchical
form, that, along with data in the non-hierarchical query component
(Section 3.4), serves to identify a resource within the scope of the
URI's scheme and naming authority (if any).  The path is terminated
by the first question mark ("?") or number sign ("#") character, or
by the end of the URI.

 If a URI contains an authority component, then the path component
must either be empty or begin with a slash ("/") character.  If a URI
does not contain an authority component, then the path cannot begin
with two slash characters ("//").  In addition, a URI reference
(Section 4.1) may be a relative-path reference, in which case the
first path segment cannot contain a colon (":") character.  The ABNF
requires five separate rules to disambiguate these cases, only one of
which will match the path substring within a given URI reference.  We
use the generic term "path component" to describe the URI substring
matched by the parser to one of these rules.
	
	  path          = path-abempty    ; begins with "/" or is empty
	                    / path-absolute   ; begins with "/" but not "//"
	                    / path-noscheme   ; begins with a non-colon segment
	                    / path-rootless   ; begins with a segment
	                    / path-empty      ; zero characters
		
	  path-abempty  = *( "/" segment )
	  path-absolute = "/" [ segment-nz *( "/" segment ) ]
	  path-noscheme = segment-nz-nc *( "/" segment )
	  path-rootless = segment-nz *( "/" segment )
	  path-empty    = 0<pchar>


      segment       = *pchar
      segment-nz    = 1*pchar
      segment-nz-nc = 1*( unreserved / pct-encoded / sub-delims / "@" )
                    ; non-zero-length segment without any colon ":"

      pchar         = unreserved / pct-encoded / sub-delims / ":" / "@"
      
A path consists of a sequence of path segments separated by a slash
("/") character.  A path is always defined for a URI, though the
defined path may be empty (zero length).  Use of the slash character
to indicate hierarchy is only required when a URI will be used as the
context for relative references.  For example, the URI
<mailto:fred@example.com> has a path of "fred@example.com", whereas
the URI <foo://info.example.com?fred> has an empty path.

The path segments "." and "..", also known as dot-segments, are
defined for relative reference within the path name hierarchy.  They
are intended for use at the beginning of a relative-path reference
(Section 4.2) to indicate relative position within the hierarchical
tree of names.  This is similar to their role within some operating
systems' file directory structures to indicate the current directory
and parent directory, respectively.  However, unlike in a file
system, these dot-segments are only interpreted within the URI path
hierarchy and are removed as part of the resolution process (Section
5.2).

Aside from dot-segments in hierarchical paths, a path segment is
considered opaque by the generic syntax.  URI producing applications
often use the reserved characters allowed in a segment to delimit
scheme-specific or dereference-handler-specific subcomponents.  For
example, the semicolon (";") and equals ("=") reserved characters are
often used to delimit parameters and parameter values applicable to
that segment.  The comma (",") reserved character is often used for
similar purposes.  For example, one URI producer might use a segment
such as "name;v=1.1" to indicate a reference to version 1.1 of
"name", whereas another might use a segment such as "name,1.1" to
indicate the same.  Parameter types may be defined by scheme-specific
semantics, but in most cases the syntax of a parameter is specific to
the implementation of the URI's dereferencing algorithm.

### query

The query component contains non-hierarchical data that, along with
data in the path component (Section 3.3), serves to identify a
resource within the scope of the URI's scheme and naming authority
(if any).  The query component is indicated by the first question
mark ("?") character and terminated by a number sign ("#") character
or by the end of the URI.

	query       = *( pchar / "/" / "?" )
	
The characters slash ("/") and question mark ("?") may represent data
within the query component.  Beware that some older, erroneous
implementations may not handle such data correctly when it is used as
the base URI for relative references (Section 5.1), apparently
because they fail to distinguish query data from path data when
looking for hierarchical separators.  However, as query components
are often used to carry identifying information in the form of
"key=value" pairs and one frequently used value is a reference to
another URI, it is sometimes better for usability to avoid percent-
encoding those characters.

### fragment

The fragment identifier component of a URI allows indirect
identification of a secondary resource by reference to a primary
resource and additional identifying information.  The identified
secondary resource may be some portion or subset of the primary
resource, some view on representations of the primary resource, or
some other resource defined or described by those representations.  A
fragment identifier component is indicated by the presence of a
number sign ("#") character and terminated by the end of the URI.

  	fragment    = *( pchar / "/" / "?" )
  	
The semantics of a fragment identifier are defined by the set of
representations that might result from a retrieval action on the
primary resource.  The fragment's format and resolution is therefore
dependent on the media type [RFC2046] of a potentially retrieved
representation, even though such a retrieval is only performed if the
URI is dereferenced.  If no such representation exists, then the
semantics of the fragment are considered unknown and are effectively
unconstrained.  Fragment identifier semantics are independent of the
URI scheme and thus cannot be redefined by scheme specifications.

### http URI Scheme

The "http" URI scheme is hereby defined for the purpose of minting
identifiers according to their association with the hierarchical
namespace governed by a potential HTTP origin server listening for
TCP ([RFC0793]) connections on a given port.

	 http-URI = "http:" "//" authority path-abempty [ "?" query ]
	            [ "#" fragment ]
	            
### https URI Scheme


The "https" URI scheme is hereby defined for the purpose of minting
identifiers according to their association with the hierarchical
namespace governed by a potential HTTP origin server listening to a
given TCP port for TLS-secured connections ([RFC5246]).

	 https-URI = "https:" "//" authority path-abempty [ "?" query ]
	             [ "#" fragment ]
	             
Representation header fields provide metadata about the
representation.  When a message includes a payload body, the
representation header fields describe how to interpret the
representation data enclosed in the payload body.  In a response to a
HEAD request, the representation header fields describe the
representation data that would have been enclosed in the payload body
if the same request had been a GET.

## Message Format

All HTTP/1.1 messages consist of a start-line followed by a sequence
of octets in a format similar to the Internet Message Format
[RFC5322]: zero or more header fields (collectively referred to as
the "headers" or the "header section"), an empty line indicating the
end of the header section, and an optional message body.

     HTTP-message   = start-line
                  *( header-field CRLF )
                  CRLF
                  [ message-body ]

The normal procedure for parsing an HTTP message is to read the
start-line into a structure, read each header field into a hash table
by field name until the empty line, and then use the parsed data to
determine if a message body is expected.  If a message body has been
indicated, then it is read as a stream until an amount of octets
equal to the message body length is read or the connection is closed.

### Start Line

An HTTP message can be either a request from client to server or a
response from server to client.  Syntactically, the two types of
message differ only in the start-line, which is either a request-line
(for requests) or a status-line (for responses), and in the algorithm
for determining the length of the message body (Section 3.3).

In theory, a client could receive requests and a server could receive
responses, distinguishing them by their different start-line formats,
but, in practice, servers are implemented to only expect a request (a
response is interpreted as an unknown or invalid request method) and
clients are implemented to only expect a response.

	start-line     = request-line / status-line

#### Request Line

A request-line begins with a method token, followed by a single space
(SP), the request-target, another single space (SP), the protocol
version, and ends with CRLF.

	request-line   = method SP request-target SP HTTP-version CRLF
	
	method         = token

#### Status Line

The first line of a response message is the status-line, consisting
of the protocol version, a space (SP), the status code, another
space, a possibly empty textual phrase describing the status code,
and ending with CRLF.

	status-line = HTTP-version SP status-code SP reason-phrase CRLF
	
	status-code    = 3DIGIT
	
	reason-phrase  = *( HTAB / SP / VCHAR / obs-text )
	
## Header Fields
